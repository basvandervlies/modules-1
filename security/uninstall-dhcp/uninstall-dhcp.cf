bundle agent uninstall_dhcp
# @brief Makes sure the dhcp packages are not installed on the system.
# Based on the OpenSCAP Security Guide for RHEL 7:
# https://static.open-scap.org/ssg-guides/ssg-rhel7-guide-anssi_nt28_minimal.html
# CFEngine policy based on this module:
# https://build.cfengine.com/modules/uninstall-ftp/
{
  vars:
    redhat|suse::
      "pkg_name" slist => { "dhcp" }; # Name in yum
    debian::
      "pkg_name" slist => { "isc-dhcp-server" }; # name in apt

  classes:
      "dhcp_allowed"
        or => {
          "hardening_dhcp_allowed",
          "data:hardening_dhcp_allowed",
          "exception_uninstall_dhcp",
          "data:exception_uninstall_dhcp",
        };

  packages:
    !dhcp_allowed::
      "${pkg_name}"
        policy => "absent",
        if => isvariable("pkg_name");

  reports:
    !dhcp_allowed::
      "warning: dhcp package name not known for this platform"
        unless => isvariable("pkg_name");
}
