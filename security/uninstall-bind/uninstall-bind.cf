bundle agent uninstall_bind
# @brief Makes sure the bind packages are not installed on the system.
# Based on the OpenSCAP Security Guide for RHEL 7:
# https://static.open-scap.org/ssg-guides/ssg-rhel7-guide-anssi_nt28_minimal.html
# CFEngine policy based on this module:
# https://build.cfengine.com/modules/uninstall-ftp/
{
  vars:
    redhat|suse::
      "pkg_name" slist => { "bind" }; # Name in yum
    debian::
      "pkg_name" slist => { "bind" }; # name in apt

  classes:
      "bind_allowed"
        or => {
          "hardening_bind_allowed",
          "data:hardening_bind_allowed",
          "exception_uninstall_bind",
          "data:exception_uninstall_bind",
        };

  packages:
    !bind_allowed::
      "${pkg_name}"
        policy => "absent",
        if => isvariable("pkg_name");

  reports:
    !bind_allowed::
      "warning: bind package name not known for this platform"
        unless => isvariable("pkg_name");
}
