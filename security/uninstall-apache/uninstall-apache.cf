bundle agent uninstall_apache
# @brief Makes sure the apache packages are not installed on the system.
# Based on the OpenSCAP Security Guide for RHEL 7:
# https://static.open-scap.org/ssg-guides/ssg-rhel7-guide-anssi_nt28_minimal.html
# CFEngine policy based on this module:
# https://build.cfengine.com/modules/uninstall-ftp/
{
  vars:
    redhat|suse::
      "pkg_name" slist => { "httpd" }; # Name in yum
    debian::
      "pkg_name" slist => { "apache2" }; # name in apt

  classes:
      "apache_allowed"
        or => {
          "hardening_apache_allowed",
          "data:hardening_apache_allowed",
          "exception_uninstall_apache",
          "data:exception_uninstall_apache",
        };

  packages:
    !apache_allowed::
      "${pkg_name}"
        policy => "absent",
        if => isvariable("pkg_name");

  reports:
    !apache_allowed::
      "warning: apache package name not known for this platform"
        unless => isvariable("pkg_name");
}
