bundle agent uninstall_squid
# @brief Makes sure the squid packages are not installed on the system.
# Based on the OpenSCAP Security Guide for RHEL 7:
# https://static.open-scap.org/ssg-guides/ssg-rhel7-guide-anssi_nt28_minimal.html
# CFEngine policy based on this module:
# https://build.cfengine.com/modules/uninstall-ftp/
{
  vars:
    redhat|suse::
      "pkg_name" slist => { "squid" }; # Name in yum
    debian::
      "pkg_name" slist => { "squid" }; # name in apt

  classes:
      "squid_allowed"
        or => {
          "hardening_squid_allowed",
          "data:hardening_squid_allowed",
          "exception_uninstall_squid",
          "data:exception_uninstall_squid",
        };

  packages:
    !squid_allowed::
      "${pkg_name}"
        policy => "absent",
        if => isvariable("pkg_name");

  reports:
    !squid_allowed::
      "warning: squid package name not known for this platform"
        unless => isvariable("pkg_name");
}
